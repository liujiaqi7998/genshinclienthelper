.版本 2
.支持库 eAPI

.程序集 窗口程序集_启动窗口
.程序集变量 代理程序目录, 文本型
.程序集变量 访问对象, WinHttp_中文版

.子程序 _按钮1_被单击

.如果 (按钮1.标题 ＝ “启动代理”)
    启动代理 ()
.否则
    .如果真 (按钮1.标题 ＝ “结束代理”)
        结束代理 ()
    .如果真结束

.如果结束





.子程序 结束代理

设置IELAN代理 (“”)
终止进程 (“ProxyMain.exe”)
状态标签.标题 ＝ “当前状态：已停止”
按钮1.标题 ＝ “启动代理”


.子程序 启动代理
.局部变量 前缀, 文本型
.局部变量 返回文本, 文本型
.局部变量 全部证书, 文本型

结束代理 ()
按钮1.标题 ＝ “请稍后”

运行控制台程序 (代理程序目录 ＋ “CertMgr.exe /s Root”, 全部证书, , )
.如果真 (寻找文本 (全部证书, “2F EE DB 2E 1E 93 7E C5 5E 7B CB 65 8C 6B B2 90 D3 1C 22 6A”, , 假) ＜ 0)
    信息框 (“您还没有安装信任证书证书，点击确定自动安装”, 0, , )
    系统_以管理员模式创建进程 (代理程序目录 ＋ “install_crt.bat”, , )
.如果真结束



.如果真 (编辑框1.内容 ＝ “”)
    信息框 (“地址不能为空”, 0, , )
    返回 ()
.如果真结束
.如果真 (到整数 (编辑框2.内容) ＜ 1 或 到整数 (编辑框2.内容) ＞ 65535)
    信息框 (“端口号输入有误”, 0, , )
    返回 ()
.如果真结束
写配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “REMOTE_HOST”, 编辑框1.内容)
写配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “REMOTE_PORT”, 编辑框2.内容)

.如果 (选择框1.选中)
    写配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “USE_SSL”, “1”)
    前缀 ＝ “https://”
.否则
    写配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “USE_SSL”, “0”)
    前缀 ＝ “http://”
.如果结束

状态标签.标题 ＝ “当前状态：正在测试你设置的服务器是否正常”
访问对象.属性 (4, 13056, , , )
访问对象.打开 (“GET”, 前缀 ＋ 编辑框1.内容 ＋ “:” ＋ 编辑框2.内容, 假)
访问对象.发送 ()
返回文本 ＝ 访问对象.取返回文本 ()
访问对象.中止 ()

.如果 (取文本长度 (返回文本) ＞ 2)
    设置IELAN代理 (“127.0.0.1:12735”)
    运行 (代理程序目录 ＋ “ProxyMain.exe”, 假, #隐藏窗口)
    程序_延时 (3, 1)

    状态标签.标题 ＝ “当前状态：启动完成，正在测试连接是否正常”
    访问对象.设置代理 (, “127.0.0.1:12735”, )
    访问对象.打开 (“GET”, “https://account.mihoyo.com”, 假)
    访问对象.发送 ()
    返回文本 ＝ 访问对象.取返回文本 ()
    访问对象.中止 ()
    .如果 (取文本长度 (返回文本) ＞ 2)
        状态标签.标题 ＝ “当前状态：启动成功，游戏过程中请勿结束代理”
        按钮1.标题 ＝ “结束代理”
    .否则
        信息框 (“无法连接到游戏服务器，可能是代理故障”, 0, , )

    .如果结束

.否则
    状态标签.标题 ＝ “当前状态：启动失败，你设置的服务器无法访问”
    结束代理 ()
    信息框 (“启动失败，你设置的服务器无法访问”, 0, , )
.如果结束




.子程序 __启动窗口_创建完毕

程序_禁止重复运行 (“qweefsdghrgxds”, 真, “已经启动了，请不要重复运行”, , 真)
代理程序目录 ＝ 取运行目录 () ＋ “\proxy\”
编辑框1.内容 ＝ 读配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “REMOTE_HOST”, “127.0.0.1”)
编辑框2.内容 ＝ 读配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “REMOTE_PORT”, “443”)
.如果 (读配置项 (代理程序目录 ＋ “config.ini”, “Proxy”, “USE_SSL”, “1”) ＝ “1”)
    选择框1.选中 ＝ 真
.否则
    选择框1.选中 ＝ 假
.如果结束




.子程序 设置IELAN代理, , , 设置LAN的http代理
.参数 代理地址, 文本型, 可空, 比如"202.101.224.69:8080",本参数为空取消代理设置

写注册项 (3, “Software\Microsoft\Windows\CurrentVersion\Internet Settings\ProxyServer”, 代理地址)
.如果 (代理地址 ＝ “”)
    写注册项 (3, “Software\Microsoft\Windows\CurrentVersion\Internet Settings\ProxyEnable”, 0)
.否则
    写注册项 (3, “Software\Microsoft\Windows\CurrentVersion\Internet Settings\ProxyEnable”, 1)
.如果结束


.子程序 __启动窗口_将被销毁

结束代理 ()

.子程序 _按钮2_被单击

运行 (取运行目录 () ＋ “\GrasscutterTools-v0.10.0-M1.exe”, 假, )

